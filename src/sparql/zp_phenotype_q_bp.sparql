prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?s ?lab ?quality ?bearer WHERE 
{
?s rdfs:subClassOf* <http://purl.obolibrary.org/obo/ZP_0000000> . 
?s owl:equivalentClass [ 
	rdf:type owl:Restriction ;
	owl:onProperty <http://purl.obolibrary.org/obo/BFO_0000051> ;
	owl:someValuesFrom [ 
		owl:intersectionOf ( ?quality
			[ rdf:type owl:Restriction ;
        owl:onProperty <http://purl.obolibrary.org/obo/RO_0000052> ;
        owl:someValuesFrom ?bearer ]
       [ rdf:type owl:Restriction ;
         owl:onProperty <http://purl.obolibrary.org/obo/RO_0002573> ;
         owl:someValuesFrom <http://purl.obolibrary.org/obo/PATO_0000460> ]
    )
	] 
] . 
OPTIONAL { ?s rdfs:label ?lab }
}